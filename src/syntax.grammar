@top LiquidTemplate { template* }

template { Output | tagBlock | HTML }
forTemplate { template | tag<ContinueTag> | tag<BreakTag> }
tagBlock {
    tag<AssignTag | BlockTag | CycleTag | IncrementTag | DecrementTag | EchoTag | IncludeTag | RenderTag | LayoutTag | LiquidTag | UnknownTag> |
    CaptureBlock {
        tag<CaptureTag>
        template*
        tag<EndCaptureTag>
    } |
    CaseBlock {
        tag<CaseTag>
        (tag<WhenTag> template*)?
        (tag<ElseTag> template*)?
        tag<EndCaseTag>
    } |
    ForBlock {
        tag<ForTag> forTemplate*
        (tag<ElseTag> forTemplate*)?
        tag<EndForTag>
    } |
    IfBlock {
        tag<IfTag> template*
        (tag<ElsifTag> template*)?
        (tag<ElseTag> template*)?
        tag<EndIfTag>
    } |
    TableRowBlock {
        tag<TableRowTag> template*
        (tag<ElseTag> template*)?
        tag<EndTableRowTag>
    }
}
@skip { space } {
    innerTag { AssignTag | BlockTag | BreakTag | CaptureTag | EndCaptureTag | CaseTag | WhenTag | EndCaseTag | ContinueTag | CycleTag | DecrementTag | EchoTag | ForTag | EndForTag | IfTag | ElsifTag | ElseTag | EndIfTag | IncludeTag | IncrementTag | InlineCommentTag | LayoutTag | RenderTag | TableRowTag | EndTableRowTag | UnlessTag | UnknownTag }
    AssignTag { tagName<"assign"> VariableDefinition "=" filteredValue }
    BlockTag { tagName<"block"> Label }
    BreakTag { tagName<"break"> }
    CaptureTag { tagName<"capture"> VariableDefinition }
    EndCaptureTag { tagName<"endcapture"> }
    CaseTag { tagName<"case"> value }
    WhenTag { tagName<"when"> value }
    EndCaseTag { tagName<"endcase"> }
    ContinueTag { tagName<"continue"> }
    CycleTag { tagName<"cycle"> (Label ":")? value ("," value)* }
    DecrementTag { tagName<"decrement"> (VariableName | MemberExpression) }
    EchoTag { tagName<"echo"> filteredValue }
    ForTag { tagName<"for"> VariableDefinition kw<"in"> value (arg<"reversed"> | kv<"offset"> | kv<"limit">)* tagArg* }
    EndForTag { tagName<"endfor"> }
    IfTag { tagName<"if"> value }
    ElsifTag { tagName<"elsif"> value }
    ElseTag { tagName<"else"> value }
    EndIfTag { tagName<"endif"> value }
    IncludeTag { tagName<"include"> value (renderArgument ("," renderArgument)*)? }
    IncrementTag { tagName<"increment"> (VariableName | MemberExpression) }
    InlineCommentTag { tagName<"#"> UnknownTagArg? }
    LayoutTag { tagName<"layout"> value (renderArgument ("," renderArgument)*)? }
    RenderTag { tagName<"render"> value (renderArgument ("," renderArgument)*)? }
    TableRowTag { tagName<"tablerow"> VariableDefinition kw<"in"> value (kv<"cols"> | kv<"offset"> | kv<"limit"> | ArgumentName | KeyValuePair)* }
    EndTableRowTag { tagName<"endtablerow"> }
    UnlessTag { tagName<"unless"> value }
    UnknownTag { unknownTagName (UnknownTagArg)* }
    LiquidTag { tagName<"liquid"> (linebreak+ innerTag)* linebreak* }

    value { VariableName | MemberExpression | literal | Range }
    literal { Quoted | Number | Boolean | DropLiteral | Null }
	VariableName { identifier }
	VariableDefinition { identifier }
    Label { identifier }
	PropertyName { identifier }
	MemberExpression { value ("." PropertyName | "[" value "]") | "[" value "]"}
    Range { "(" value ".." value ")"}

    ArgumentName { identifier }
    KeyValuePair { ArgumentName ":" value }
    kv<T> { arg<T> ":" value }
    renderArgument { kw<"with"> ":"? value | KeyValuePair }
    filteredValue { value (Filter)* }
    Output { OutputStart filteredValue OutputEnd }

    Filter { "|" FilterName ":" FilterArgs }
    FilterName { identifier }
    FilterArgs { FilterArg ("," FilterArg)* }
    FilterArg { value | KeyValuePair }

    tag[@name="Tag"]<T> { TagStart T TagEnd }
    tagName[@name="TagName"]<T> { @specialize<identifier, T> }
    unknownTagName[@name="TagName"] { identifier }
    tagArg { ArgumentName | KeyValuePair }
}

arg[@name="ArgumentName"]<type> { @specialize[@name={type}]<identifier, type> }
kw[@name={type}]<type> { @specialize<identifier, type> }
Boolean { @specialize<identifier, "true" | "false"> }
Null { @specialize<identifier, "nil" | "null"> }
DropLiteral { @specialize<identifier, "empty" | "blank" | "forloop"> }

@tokens {
    TagStart[closedBy=TagEnd] { "{%" | "{%-" }
    TagEnd[openedBy=TagStart] { "%}" | "-%}" }
    OutputStart[closedBy=OutputEnd] { "{{" | "{{-" }
    OutputEnd[openedBy=OutputStart] { "}}" | "-}}" }

    identifier { @asciiLetter (@asciiLetter | @digit | "_" | "#")* }
    Number { @digit+ ("." @digit+)? }
    Quoted { "\"" (!["] | '\\"')* "\"" | "'" (!['] | "\\'")* "'"}
    HTML { (![{] | "{" ![{%])+ }
    UnknownTagArg { (![%\r\n] | "%" ![}\r\n] | "%" @eof )+ }

    "["
    "]"
    ","
    "."
    ":"
    "="
    ".."
    linebreak { "\r"? "\n" }
    space { (" " | "\t")+ }

    @precedence { TagStart, OutputStart, TagEnd, OutputEnd, Quoted, Number, identifier, space, "..", "=", ":", ".", "[", "]", UnknownTagArg }
}
